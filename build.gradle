plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.7.1'
    id "io.github.goooler.shadow" version '8.1.8'
}

group = 'no.vestlandetmc'
version = '3.0.7'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.glaremasters.me/repository/bloodshot/' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://repo.essentialsx.net/releases/' }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.4'
    compileOnly 'com.griefdefender:api:2.1.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.10'

    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.9.0-4'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'

    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'org.jetbrains:annotations:24.1.0'
}

shadowJar {
    relocate 'org.bstats', 'no.vestlandetmc.elevator.bstats'
    relocate 'org.jetbrains', 'no.vestlandetmc.elevator.annotations'
    archiveFileName.set('Elevator.jar')
}

jar.dependsOn shadowJar

jar {
    enabled = false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
